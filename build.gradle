buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.14.2'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 21
    buildToolsVersion "20.0.0"
    publishNonDefault true

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 21
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}

android.libraryVariants.all { variant ->
    if (variant.buildType.name == "release") {
        variant.outputFile = new File(variant.outputFile.parentFile, variant.outputFile.name.replace("release.aar", VERSION_NAME + ".aar"));
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:21.+'
    compile 'com.android.support:mediarouter-v7:21.+'
    compile 'com.google.android.gms:play-services:6.1.+'
}

publishing {
    publications {
        release(MavenPublication) {
            artifact source: file("${project.buildDir}/outputs/aar/${project.name}-${VERSION_NAME}.aar"),extension:"aar"
            groupId GROUP
            artifactId POM_ARTIFACT_ID
            version VERSION_NAME

            artifact androidSourcesJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "${project.VERSION_NAME.endsWith('-SNAPSHOT') ? SNAPSHOT_REPOSITORY_URL : RELEASE_REPOSITORY_URL}"
        }
    }
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}