/*
 * Copyright (C) 2015 Google Inc. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 22
    buildToolsVersion "23"
    publishNonDefault true

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 22
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}

android.libraryVariants.all { variant ->
    if (variant.buildType.name == "release") {
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parentFile, output.outputFile.name.replace("release.aar", VERSION_NAME + ".aar"));
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.+'
    compile 'com.android.support:mediarouter-v7:22.2.+'
    compile 'com.google.android.gms:play-services-cast:7.8.+'
}

publishing {
    publications {
        release(MavenPublication) {
            artifact source: file("${project.buildDir}/outputs/aar/${project.name}-${VERSION_NAME}.aar"),extension:"aar"
            groupId GROUP
            artifactId POM_ARTIFACT_ID
            version VERSION_NAME

            artifact androidSourcesJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "${project.VERSION_NAME.endsWith('-SNAPSHOT') ? SNAPSHOT_REPOSITORY_URL : RELEASE_REPOSITORY_URL}"
        }
    }
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}
